apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33
    buildToolsVersion "34.0.0"
    useLibrary 'org.apache.http.legacy'
    flavorDimensions "versionCode"
    defaultConfig {
        applicationId 'com.samourai.wallet'
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 199
        versionName "0.99.98i++"
        // Enabling multidex support.
        multiDexEnabled true
        vectorDrawables {
            useSupportLibrary true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "XOR_1", "\""+getApiKey("XOR_1")+"\""
            variant.buildConfigField "String", "XOR_2", "\""+getApiKey("XOR_2")+"\""
        }
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/{AL2.0,LGPL2.1}', 'META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1', 'META-INF/proguard/androidx-annotations.pro']
        }
    }
    productFlavors {
        production {
            applicationId 'com.samourai.wallet'
            versionCode 199
            versionName "0.99.98i++"
            resValue "string", "app_name", "Samourai"
            resValue "string", "version_name", "0.99.98i++"
        }
        staging {
            applicationId 'com.samourai.wallet.staging'
            versionCode 199
            versionName "0.99.98i++"
            resValue "string", "app_name", "Samourai Staging"
            resValue "string", "version_name", "0.99.98i++"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/kotlinx_coroutines_core.version'
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.samourai.wallet'
}

def getApiKey(key){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('samourai.api')))
    return props[key]
}


dependencies {

    def lifecycle_version = "2.5.0"
    def topl_android_version = "2.1.2"

    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.google.zxing:core:3.4.1') {
        transitive = true
    }

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.core:core-ktx:1.8.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "androidx.webkit:webkit:1.5.0"
    implementation 'androidx.compose.material3:material3:1.0.0-alpha02'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-codec:commons-codec:1.4'
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    implementation 'com.google.android.material:material:1.7.0-alpha02'
    implementation 'org.hibernate:hibernate-validator:5.4.3.Final'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'
    implementation ('io.samourai.code.whirlpool:whirlpool-client:1.0.1') {
        exclude group:'org.hibernate', module:'hibernate-validator'
        exclude group:'com.google.code.findbugs', module:'jsr305'
        exclude group:'com.google.protobuf', module:'protobuf-java'
        exclude group:'net.jcip', module:'jcip-annotations'
        exclude group:'com.squareup.okhttp', module:'okhttp'
        exclude group:'com.google.guava', module:'guava'
        //
        exclude group:'org.json', module:'json'
        exclude group:'io.samourai.code.wallet', module:'extlibj'
    }
    implementation ('io.samourai.code.wallet:extlibj:1.0.1') {
        exclude group:'com.google.code.findbugs', module:'jsr305'
        exclude group:'com.google.protobuf', module:'protobuf-java'
        exclude group:'net.jcip', module:'jcip-annotations'
        exclude group:'com.squareup.okhttp', module:'okhttp'
        exclude group:'com.google.guava', module:'guava'
        //
        exclude group:'org.json', module:'json'
    }

    implementation ('io.samourai.code.wallet:boltzmann-java:1.0.6') {
        exclude group: 'it.unimi.dsi', module: 'fastutil'
    }
    // temporary fix for building boltzmann-java dependency
    implementation 'it.unimi.dsi:fastutil:7.2.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.auth0.android:jwtdecode:1.1.1'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'io.samourai.code.wallet:android-tor-binary:0.4.7.12'

    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    testImplementation 'junit:junit:4.12'

    implementation "io.matthewnelson.topl-android:topl-core-base:$topl_android_version"
    implementation "io.matthewnelson.topl-android:topl-service:$topl_android_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'io.samourai.code.wallet:hummingbird-android:0.9'
    implementation 'com.sparrowwallet:hummingbird:1.6.6'

    //JetpackCompose
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.5.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.1.1'
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    // Animations
    implementation 'androidx.compose.animation:animation:1.1.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.1'
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "com.google.accompanist:accompanist-insets:0.24.9-beta"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}
repositories {
    mavenCentral()
}
